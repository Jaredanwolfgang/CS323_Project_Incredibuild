use spl_lexer::tokens::{Token, LexicalError};
use spl_ast::tree;
use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, Token, LexicalError>>);

extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token {
    "identifier" => Token::Identifier(<String>),
    "int" => Token::LiteralInt(<i32>),
    "float" => Token::LiteralFloat(<f32>),
    "bool" => Token::LiteralBool(<bool>),
    "char" => Token::LiteralChar(<char>),
    "(" => Token::LeftParen,
    ")" => Token::RightParen,
    "+" => Token::OpPlus,
    "-" => Token::OpMinus,
    "*" => Token::OpMul,
    "/" => Token::OpDiv,
    "^" => Token::OpPow,
    "%" => Token::OpMod,
  }
}

pub Expr: Box<tree::CompExpr> = {
#[precedence(level="1")]
    Term,

#[precedence(level="2")] #[assoc(side="left")]
    <lhs:Expr> "^" <rhs:Expr> => {
      Box::new(tree::CompExpr::BinaryOperation (
        lhs, tree::BinaryOperator::Pow, rhs
      ))
    },

    <lhs:Expr> "%" <rhs:Expr> => {
      Box::new(tree::CompExpr::BinaryOperation (
        lhs, tree::BinaryOperator::Mod, rhs
      ))
    },

#[precedence(level="3")] #[assoc(side="left")]
    <lhs:Expr> "*" <rhs:Expr> => {
      Box::new(tree::CompExpr::BinaryOperation (
        lhs, tree::BinaryOperator::Mul, rhs
      ))
    },

    <lhs:Expr> "/" <rhs:Expr> => {
      Box::new(tree::CompExpr::BinaryOperation (
        lhs, tree::BinaryOperator::Div, rhs
      ))
    },

#[precedence(level="4")] #[assoc(side="left")]
    <lhs:Expr> "+" <rhs:Expr> => {
      Box::new(tree::CompExpr::BinaryOperation (
        lhs, tree::BinaryOperator::Add, rhs
      ))
    },

    <lhs:Expr> "-" <rhs:Expr> => {
      Box::new(tree::CompExpr::BinaryOperation (
        lhs, tree::BinaryOperator::Sub, rhs
      ))
    },
}

Term: Box<tree::CompExpr> = {
    <n: "int"> => Box::new(tree::CompExpr::Value(tree::Value::Integer(n))),
    <n: "float"> => Box::new(tree::CompExpr::Value(tree::Value::Float(n))),
    <n: "bool"> => Box::new(tree::CompExpr::Value(tree::Value::Bool(n))),
    <n: "char"> => Box::new(tree::CompExpr::Value(tree::Value::Char(n))),
    "(" <Expr> ")",
    ! => { errors.push(<>); Box::new(tree::CompExpr::Error) },
}
