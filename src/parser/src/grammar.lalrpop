use spl_lexer::tokens::{Token, LexicalError};
use spl_ast::tree;
use lalrpop_util::ErrorRecovery;
use lalrpop_util::ParseError;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, Token, LexicalError>>);

pub Program: tree::Program = {
    <prg:ProgramPart*> => {
        tree::Program::Program(
            prg
        )
    }
}

ProgramPart: tree::ProgramPart = {
    <stmt: Stmt> => tree::ProgramPart::Statement(Box::new(stmt)),
    <func: FuncDec> => tree::ProgramPart::Function(func)
}

// Some useful declarations in the beginning of the program
pub ParaDecs = Comma<ParaDec>;
ArgList = Comma<CompExpr>;
VarDecs = Comma<VarDec>;

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

ParaDec: tree::Variable = {
    <spec:Specifier> <name:"identifier"> => {
        let mut values = Vec::new();
        values.push(spec);
        tree::Variable::FormalParameter(
            Box::new(name),
            Box::new(values),
            Box::new(Vec::new())
        )
    }
}

DimDecs: Vec<tree::CompExpr> = {
    "[" <n:CompExpr> "]" => {
        let mut v = Vec::new();
        v.push(*n);
        v
    },
    "[" <n:CompExpr> "]" <size:DimDecs> => {
        let mut v = Vec::new();
        v.push(*n);
        v.extend(size);
        v
    }
}

/*
* Statements
* 1. Struct definition: struct identifier { fields };
* 2. Include statement: include "string";
* 3. Global variable declaration: Specifier VarDec;
* 4. TODO: macro definition 
*/
pub Stmt: tree::Statement = {
    "struct" <name:"identifier"> "{" <fields:FieldsDec> "}" ";" => {
        tree::Statement::Struct(
            tree::Variable::StructDefinition(
                Box::new(name),
                Box::new(fields)
            )
        )
    },
    "include" <name:"string"> ";" => {
        tree::Statement::Include(
            Box::new(name)
        )
    },
    <global: VarDef> ";"=> {
        tree::Statement::GlobalVariable(
            global
        )
    },

}

/*
* Fields declaration: Used in struct definition 
*/
FieldsDec: Vec<tree::Variable> = {
    <field:VarDef> <l:@L> <comma:";"?> => {
        if comma == None {
            let error = ErrorRecovery {
                error: ParseError::User {
                    error: LexicalError::MissingLexeme("token \";\"".to_string(), l)
                },
                dropped_tokens: Vec::new(), // or specify the dropped tokens
            };
            errors.push(error);
        }
        field
    },
    <mut fields:FieldsDec> <v:VarDef> ";" => {
        fields.extend(v);
        fields
    },
}

/*
* Function declaration:
* 1. Function declaration: Specifier identifier (ParaDecs) { Body }
* 2. Function call: identifier (ArgList);
* 3. Body: { Expr* }
*/
pub FuncDec: Box<tree::Function> = {
    <ret: Specifier> <name:"identifier"> <lb:"("> <params:ParaDecs> <l:@L><rb:")"?> "{" <body:Body> "}" => {
        if rb == None {
            let error = ErrorRecovery {
                error: ParseError::User {
                    error: LexicalError::MissingLexeme("token \")\"".to_string(), l)
                },
                dropped_tokens: Vec::new(), // or specify the dropped tokens
            };
            errors.push(error);
        }
        Box::new(tree::Function::FuncDeclaration(
            Box::new(name),
            Box::new(params),
            Box::new(ret),
            body
        ))
    },
    ! => { errors.push(<>); Box::new(tree::Function::Error) },
}

FuncCall: tree::Expr = {
    <name:"identifier"> "(" <args:ArgList> ")" ";" => {
        tree::Expr::FuncCall(
            tree::Function::FuncReference(
                Box::new(name),
                Box::new(args)
            )
        )
    }
}

/*
* Body is a list of expressions
* Expressions can be:
* 1. If statement: if (CondExpr) { Body } else { Body }
* 2. While loop: while (CondExpr) { Body }
* 3. For loop: for (VarManagement; CondExpr; VarManagement) { Body }
* 4. Variable management: VarManagement;
* 5. Function call: FuncCall;
* 6. Break statement: break;
* 7. Continue statement: continue;
* 8. Return statement: return CompExpr;
*/

pub Body: tree::Body = {
    <stmt:Expr*> => tree::Body::Body(stmt)
}

Expr: tree::Expr = {
    <expr:IfExpr> => expr,
    <expr:WhileExpr> => expr,
    <expr:ForExpr> => expr,
    <expr:VarManagement> ";" => expr,
    <expr:FuncCall> => expr,
    <expr:"break"> ";" => tree::Expr::Break,
    <expr:"continue"> ";" => tree::Expr::Continue,
    <expr:"return"> <val:CompExpr?> ";" => match val {
        None => tree::Expr::Return(tree::CompExpr::Value(tree::Value::Null)),
        Some(val) => tree::Expr::Return(*val)
    },
    ! => { errors.push(<>); tree::Expr::Error }
}

IfExpr: tree::Expr = {
    "if" "(" <cond:CondExpr> ")" "{" <body:Body> "}" => {
        tree::Expr::If(
            tree::If::IfExpr(
                cond,
                body
            )
        )
    },
    "if" "(" <cond:CondExpr> ")" "{" <body:Body> "}" "else" "{" <body_2:Body> "}" => {
        tree::Expr::If(
            tree::If::IfElseExpr(
                cond,
                body, 
                body_2
            )
        )
    }
}

WhileExpr: tree::Expr = {
    "while" "(" <cond:CondExpr> ")" "{" <body:Body> "}" => {
        tree::Expr::Loop(
            tree::Loop::WhileExpr(
                cond,
                body
            )
        )
    }
}

ForExpr: tree::Expr = {
    "for" "(" <init:VarManagement> ";" <cond:CondExpr> ";" <update:VarManagement> ")" "{" <body:Body> "}" => {
        tree::Expr::Loop(
            tree::Loop::ForExpr(
                Box::new(init),
                cond,
                Box::new(update),
                body
            )
        )
    }
}

VarManagement: tree::Expr = {
    /* Four types of assignment:
     * 1. Variable declaration: Specifier VarDecs;
     * 2. identifier = CompExpr;
     * 3. identifier++;
     * 4. identifier--;
    */
    #[precedence(level="1")]
    <def: VarDef> => {
        tree::Expr::VarManagement(
            def
        )
    },
    #[precedence(level="2")]
    <def: VarDec> => {
        tree::Expr::VarManagement(
            vec![def]
        )
    },
    <obj: "identifier"> "." <field: "identifier"> "=" <val:CompExpr> => {
        tree::Expr::VarManagement(
            vec![tree::Variable::StructAssignment(
                Box::new(obj.clone()),
                Box::new(field.clone()),
                val
            )]
        )
    },
    <lhs:"identifier"> <dims:DimDecs?> "++" => {
        let dims = match dims {
            Some(dims) => dims,
            None => Vec::new()
        };
        tree::Expr::VarManagement(
            vec![tree::Variable::VarAssignment(
                Box::new(lhs.clone()),
                Box::new(tree::CompExpr::BinaryOperation(
                    Box::new(tree::CompExpr::Variable(tree::Variable::VarReference(Box::new(lhs.clone()), Box::new(dims)))),
                    tree::BinaryOperator::Add,
                    Box::new(tree::CompExpr::Value(tree::Value::Integer(1)))
                )),
                Box::new(Vec::new())
            )]
        )
    },
    <lhs:"identifier"> <dims:DimDecs?> "--" => {
        let dims = match dims {
            Some(dims) => dims,
            None => Vec::new()
        };
        tree::Expr::VarManagement(
            vec![tree::Variable::VarAssignment(
                Box::new(lhs.clone()),
                Box::new(tree::CompExpr::BinaryOperation(
                    Box::new(tree::CompExpr::Variable(tree::Variable::VarReference(Box::new(lhs.clone()), Box::new(dims)))),
                    tree::BinaryOperator::Sub,
                    Box::new(tree::CompExpr::Value(tree::Value::Integer(1)))
                )),
                Box::new(Vec::new())
            )]
        )
    },
}

// Variable Definition is composed of: specifier, variable declaration;
// You should be able to declare multiple variables at once
// However, some varaibles is declared with initial value, then it should be treated as assignment
VarDef: Vec<tree::Variable> = {
    "struct" <str: "identifier"> <name:"identifier"> => {
        vec![tree::Variable::StructDeclaration(
            Box::new(str),
            Box::new(name),
            Box::new(Vec::new())
        )]
    },
    "struct" <str: "identifier"> <name:"identifier"> "{" <fields:StructDecs> "}"=> {
        vec![tree::Variable::StructDeclaration(
            Box::new(str),
            Box::new(name),
            Box::new(fields)
        )]
    },
    <spec: Specifier> <var: VarDecs> => {
        let mut variables = Vec::new();
        var.iter().for_each(|x| {
            match x {
                tree::Variable::VarDeclaration(name, values, size) => {
                    let mut values = Vec::new();
                    let mut scale = 1;
                    size.iter().for_each(|x| {
                        match x {
                            tree::CompExpr::Value(tree::Value::Integer(x)) => scale *= x,
                            _ => () // add error handling here?
                        }
                    });
                    for _ in 0..scale {
                        values.push(spec.clone());
                    }
                    variables.push(tree::Variable::VarDeclaration(name.clone(), Box::new(values), size.clone()));
                },
                tree::Variable::VarAssignment(name, value, dims) => {
                    variables.push(tree::Variable::VarDeclaration(name.clone(), Box::new(vec![spec.clone()]), Box::new(Vec::new())));
                    variables.push(tree::Variable::VarAssignment(name.clone(), value.clone(), dims.clone()));
                },
                _ => panic!("Invalid variable declaration")
            }
        });
        variables
    }
}

// Variable Declaration
// 1. identifier; => specifier ident;
// 2. identifier[size][size]...; => specifier ident[size][size]...;
// 3. identifier = CompExpr; => specifier ident = CompExpr;
// TODO: 4. identifier[size] = {values..}
VarDec: tree::Variable = {
    <ident: "identifier"> <size: DimDecs?> => {
        let dims = match size {
            Some(dims) => dims,
            None => Vec::new()
        };
        tree::Variable::VarDeclaration(
            Box::new(ident),
            Box::new(Vec::new()),
            Box::new(dims)
        )
    },
    <ident: "identifier"> <size: DimDecs?> "=" <val:CompExpr> => {
        let dims = match size {
            Some(dims) => dims,
            None => Vec::new()
        };
        tree::Variable::VarAssignment(
            Box::new(ident),
            val,
            Box::new(dims)
        )
    },
}

StructDecs = Comma<StructDec>;

StructDec: tree::Variable = {
    "." <ident: "identifier"> ":" <val: CompExpr> => {
        tree::Variable::VarAssignment(
            Box::new(ident),
            val,
            Box::new(Vec::new())
        )
    },
    ! => { errors.push(<>); tree::Variable::Error },
}

pub CondExpr: Box<tree::CondExpr> = {
#[precedence(level="1")]
    "!" <expr:CondExpr> => {
        Box::new(tree::CondExpr::UnaryCondition(
            tree::UnaryOperator::Not, expr
        ))
    },
    CondTerm,
#[precedence(level="2")]
    <lhs:CompExpr> ">" <rhs:CompExpr> => {
        Box::new(tree::CondExpr::Condition(
            lhs, tree::JudgeOperator::GT, rhs
        ))
    },
    <lhs:CompExpr> "<" <rhs:CompExpr> => {
        Box::new(tree::CondExpr::Condition(
            lhs, tree::JudgeOperator::LT, rhs
        ))
    },
    <lhs:CompExpr> ">=" <rhs:CompExpr> => {
        Box::new(tree::CondExpr::Condition(
            lhs, tree::JudgeOperator::GE, rhs
        ))
    },
    <lhs:CompExpr> "<=" <rhs:CompExpr> => {
        Box::new(tree::CondExpr::Condition(
            lhs, tree::JudgeOperator::LE, rhs
        ))
    },
    <lhs:CompExpr> "==" <rhs:CompExpr> => {
        Box::new(tree::CondExpr::Condition(
            lhs, tree::JudgeOperator::EQ, rhs
        ))
    },
    <lhs:CompExpr> "!=" <rhs:CompExpr> => {
        Box::new(tree::CondExpr::Condition(
            lhs, tree::JudgeOperator::NE, rhs
        ))
    },
#[precedence(level="3")] #[assoc(side="left")] 
    <lhs:CondExpr> "&&" <rhs:CondExpr> => {
        Box::new(tree::CondExpr::BinaryCondition(
            lhs, tree::BinaryOperator::And, rhs
        ))
    },
    <lhs:CondExpr> "||" <rhs:CondExpr> => {
        Box::new(tree::CondExpr::BinaryCondition(
            lhs, tree::BinaryOperator::Or, rhs
        ))
    },
}

pub CompExpr: Box<tree::CompExpr> = {
    #[precedence(level="1")]
    Term,

    #[precedence(level="2")] #[assoc(side="left")]
    <lhs:CompExpr> "^" <rhs:CompExpr> => {
        Box::new(tree::CompExpr::BinaryOperation (
            lhs, tree::BinaryOperator::Pow, rhs
        ))
    },

    <lhs:CompExpr> "%" <rhs:CompExpr> => {
        Box::new(tree::CompExpr::BinaryOperation (
            lhs, tree::BinaryOperator::Mod, rhs
        ))
    },

    #[precedence(level="3")] #[assoc(side="left")]
    <lhs:CompExpr> "*" <rhs:CompExpr> => {
        Box::new(tree::CompExpr::BinaryOperation (
            lhs, tree::BinaryOperator::Mul, rhs
        ))
    },

    <lhs:CompExpr> "/" <rhs:CompExpr> => {
        Box::new(tree::CompExpr::BinaryOperation (
            lhs, tree::BinaryOperator::Div, rhs
        ))
    },

    #[precedence(level="4")] #[assoc(side="left")]
    <lhs:CompExpr> "+" <rhs:CompExpr> => {
        Box::new(tree::CompExpr::BinaryOperation (
            lhs, tree::BinaryOperator::Add, rhs
        ))
    },

    <lhs:CompExpr?> "-" <rhs:CompExpr> => {
        let lhs = match lhs {
            Some(x) => x,
            None => Box::new(tree::CompExpr::Value(tree::Value::Integer(0)))
        };
        Box::new(tree::CompExpr::BinaryOperation (
            lhs, tree::BinaryOperator::Sub, rhs
        ))
    },
}

Specifier: tree::Value = {
    <t:"typeint"> => tree::Value::Integer(0),
    <t:"typefloat"> => tree::Value::Float(0.0),
    <t:"typebool"> => tree::Value::Bool(false),
    <t:"typechar"> => tree::Value::Char(' '),
    <t:"typestr"> => tree::Value::String(String::new()),
    <t:"void"> => tree::Value::Null,
}

Term: Box<tree::CompExpr> = {
    "(" <CompExpr> ")",
    <n: "identifier"> <dims:DimDecs?> => {
        match dims {
            Some(dims) => Box::new(tree::CompExpr::Variable(tree::Variable::VarReference(Box::new(n), Box::new(dims)))),
            None => Box::new(tree::CompExpr::Variable(tree::Variable::VarReference(Box::new(n), Box::new(Vec::new())))),
        }
    },
    <n: "int"> => Box::new(tree::CompExpr::Value(tree::Value::Integer(n))),
    <n: "float"> => Box::new(tree::CompExpr::Value(tree::Value::Float(n))),
    <n: "char"> => Box::new(tree::CompExpr::Value(tree::Value::Char(n))),
    <ident: "identifier"> "(" <args:ArgList> ")" => {
        Box::new(tree::CompExpr::FuncCall(
            tree::Function::FuncReference(Box::new(ident), Box::new(args))
        ))
    },
    <error: "error"> => {
        let error = ErrorRecovery {
            error: ParseError::User {
                error: LexicalError::UnknownToken
            },
            dropped_tokens: Vec::new(),
        };
        errors.push(error);
        Box::new(tree::CompExpr::Error)
    },
    ! => { errors.push(<>); Box::new(tree::CompExpr::Error) },
}

CondTerm: Box<tree::CondExpr> = {
    <n: "bool"> => Box::new(tree::CondExpr::Bool(n)),
    "(" <CondExpr> ")",
}

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token {
        "identifier" => Token::Identifier(<String>),
    
        "int" => Token::LiteralInt(<u32>),
        "float" => Token::LiteralFloat(<f32>),
        "bool" => Token::LiteralBool(<bool>),
        "char" => Token::LiteralChar(<char>),
        "string" => Token::LiteralString(<String>),
        "typeint" => Token::TypeInt,
        "typefloat" => Token::TypeFloat,
        "typebool" => Token::TypeBool,
        "typechar" => Token::TypeChar,
        "typestr" => Token::TypeString,
        "void" => Token::TypeVoid,
        "null" => Token::TypeNull,

        "(" => Token::LeftParen,
        ")" => Token::RightParen,
        "{" => Token::LeftBrace,
        "}" => Token::RightBrace,
        "[" => Token::LeftBracket,
        "]" => Token::RightBracket,
        "." => Token::Dot,
        "," => Token::Comma,
        ";" => Token::Semicolon,
        ":" => Token::Colon,

        "+" => Token::OpPlus,
        "-" => Token::OpMinus,
        "*" => Token::OpMul,
        "/" => Token::OpDiv,
        "^" => Token::OpPow,
        "%" => Token::OpMod,
        ">" => Token::OpGreaterThan,
        "<" => Token::OpLessThan,
        ">=" => Token::OpGreaterThanEqual,
        "<=" => Token::OpLessThanEqual,
        "==" => Token::OpEqual,
        "!=" => Token::OpNotEqual,
        "&&" => Token::OpAnd,
        "||" => Token::OpOr,
        "!" => Token::OpNot,
        "=" => Token::OpAssign,
        "++" => Token::OpIncrement,
        "--" => Token::OpDecrement,

        "if" => Token::KeywordIf,
        "else" => Token::KeywordElse,
        "while" => Token::KeywordWhile,
        "for" => Token::KeywordFor,
        "return" => Token::KeywordReturn,
        "break" => Token::KeywordBreak,
        "continue" => Token::KeywordContinue,

        "enum" => Token::DeclarationEnum,
        "struct" => Token::DeclarationStruct,
        "include" => Token::DeclarationInclude,

        "error" => Token::Error,
    }
}
